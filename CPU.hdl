// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    
	//determines whether it is a instruction or b instruction, 0=a
	Not(in=instruction[15],out=notin);
	Mux16(a=aluout,b=instruction,sel=notin,out=muxout);
	
	Or(a=notin,b=instruction[5],out=aout);
	ARegister(in=muxout,load=aout,out=aregout,out[0..14]=addressM);

	And(a=instruction[4],b=instruction[15],out=dout);
	DRegister(in=aluout,load=dout,out=dregout);
	
	//determines whether to use a register or inM for ALU
	And(a=instruction[12],b=instruction[15],out=out1);
	Mux16(a=aregout,b=inM,sel=out1,out=muxout2);

	And(a=instruction[3],b=instruction[15],out=writeM);	
	
	//uses d register and a register/inM for ALU
	ALU(x=dregout,y=muxout2,zx=instruction[11],nx=instruction[10],zy=instruction[9],ny=instruction[8],f=instruction[7],no=instruction[6],zr=zrout,ng=ngout,out=aluout,out=outM);
	Not(in=zrout,out=notzrout);
	Not(in=ngout,out=notngout);
	And(a=instruction[15],b=instruction[0],out=jgt); //out>0
	And(a=notngout,b=notzrout,out=notzrngout);
	And(a=jgt,b=notzrngout,out=out2);
	
	And(a=instruction[1],b=instruction[15],out=jeq); //out=0
	And(a=instruction[2],b=instruction[15],out=jlt); //out<0
	
	//determines whether output is >0, <0, or =0
	And(a=jlt,b=ngout,out=out3);
	And(a=jeq,b=zrout,out=out4);
	Or(a=out2,b=out3,out=jle);
	Or(a=jle,b=out4,out=orout);
	
	//use loadbit from previous output to get final output
	PC(in=aregout,inc=true,load=orout,reset=reset,out[0..14]=pc);
}
